import org.gradle.internal.os.OperatingSystem

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.dokka' version '0.10.1'
    id "io.codearte.nexus-staging" version "0.21.2"
}

def kotlin_version = "1.5.21"
def spek_version = "2.0.12"

project.ext.lwjglVersion = "3.2.3"
project.ext.jomlVersion = "1.9.24"


group = "com.github.kogelabs"
archivesBaseName = "ockero-jvm"
version = "0.1.1.5"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = System.getProperty("os.arch").contains("64") ? "natives-windows" : "natives-windows-x86"
        break
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    //Lwjgl dependencies
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"

    // Test dependencies
    // some version of Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"

    // spek requires kotlin-reflect, can be omitted if already in the classpath
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'org.jbox2d:jbox2d-library:2.2.1.1'


    implementation 'com.beust:klaxon:5.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// setup the test task
test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier  = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar){
    archiveClassifier = 'sources'
    inputs.dir 'src/main/kotlin'
}

artifacts {
    archives javadocJar, sourcesJar
}

//Publish to Github packages
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/KogeLabs/Ockero")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
/** Uncomment to enable upload to maven central
//Publish to maven central
 signing {
    sign configurations.archives
 }

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Koge OpenGL game engine'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Ockero (Kotlin OpenGL Game Engine) is a 2D game framework developed completely in Kotlin. Ockero have a small footprint, it is fast and easy to learn.'
                url 'https://github.com/KogeLabs/Ockero/wiki'

                scm {
                    connection 'scm:https://github.com/KogeLabs/Ockero'
                    developerConnection 'scm:git://github.com/KogeLabs/Ockero.git'
                    url 'https://github.com/KogeLabs/Ockero'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'kogelabs'
                        name 'Moncef Yabi'
                        email 'fecnom2000@hotmail.com'
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = group
    stagingProfileId = "6a84c6051dd36"
}
*/
